Phase: NameFinder
Input:
Options: control = appelt debug = true

MACRO: BEGINWITHSURNAME
(
  (
  	{SURNAME, Sentence@length < 5}
  ) |
  {SURNAME, Token.category == NR, Token@length < 5}
)

Rule: StrongNameRule
Priority:200
{SpaceToken}
{Token.string =~ "(名字|姓名)"}
({Token.category == PU} | {SpaceToken})[0,2]
(
  {PERSON} | BEGINWITHSURNAME
)
:nameLookup
{SpaceToken}
-->
:nameLookup.NAME = { kind = name, conf = strongest}

Rule: StrongNameRule
Priority:170
{SpaceToken}
{Token.string =~ "(名字|姓名)"}
({Token.category == PU} | {SpaceToken})[0,2]
(
  {Sentence@length < 10}
)
:nameLookup
{SpaceToken}
-->
:nameLookup.NAME = { kind = name, conf = strong}

Rule: NameNextToOthersRule
Priority:150
{SpaceToken.kind == control}
(
  {PERSON} | BEGINWITHSURNAME
):nameLookup
{SpaceToken.kind == control}
({Sentence, !PERSON}
 {SpaceToken.kind == control})[0,1]
({GENDER} | {MOBILE} | {CIVIL_STATE} | {BIRTHDAY} | {EMAIL} | {HUKOU} | {POLITICAL_STATUS} | {YEARS_OF_EXPERIENCE})
-->
:nameLookup.NAME = { kind = name, conf = nexttootherfeatures}

Rule: NormalNameRule
Priority:50

{SpaceToken.kind == control}
(
   {SURNAME, Sentence@length < 5, !PERSON}
)
:nameLookup
{SpaceToken.kind == control}
-->
:nameLookup.NAME = { kind = name, conf = normal}