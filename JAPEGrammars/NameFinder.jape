Phase: NameFinder
Input:
Options: control = once debug = true

MACRO: BEGINWITHSURNAME
(
  {SURNAME, Token.category == NR, Token@length < 5, Token@length > 1}
)

MACRO: POSSIBLENAME
(
  {SURNAME, Sentence@length < 5, Sentence@length > 1, !PERSON, !NAME}
)

Rule: StrongNameRule
Priority:200
({Token.string =~ "(名字|姓名)"}
({Token.category == PU} | {SpaceToken})[0,2]
(
  {PERSON} | BEGINWITHSURNAME | POSSIBLENAME
):nameLookup
):section
{SpaceToken}
-->
:nameLookup.NAME = { kind = name, conf = strongest},
:section.Section = {kind = name}

Rule: StrongNameRule
Priority:150
({Token.string =~ "(名字|姓名)"}
({Token.category == PU} | {SpaceToken})[0,2]
(
  {Sentence@length < 10}
):nameLookup
):section
{SpaceToken}
-->
:nameLookup.NAME = { kind = name, conf = strong},
:section.Section = {kind = name}

Rule: NameNextToOthersRule
Priority:100
{SpaceToken.kind == control}
(
  {PERSON} | BEGINWITHSURNAME
):nameLookup
{SpaceToken.kind == control}
({Sentence, !PERSON}
 {SpaceToken.kind == control})[0,1]
({GENDER} | {MOBILE} | {CIVIL_STATE} | {BIRTHDAY} | {EMAIL} | {HUKOU} | {POLITICAL_STATUS} | {YEARS_OF_EXPERIENCE})
-->
:nameLookup.NAME = { kind = name, conf = strong, rule = nexttootherfeatures},
:nameLookup.Section = {kind = name}

/*-------------------------------------------------------------------------*/
Phase: PossibleNameNextToOthers
Input:
Options: control = once debug = true

Rule: PossibleNameNextToOthers
Priority:100

{SpaceToken.kind == control}
(
  POSSIBLENAME
):nameLookup
{SpaceToken.kind == control}
({Sentence, !PERSON, !NAME}
 {SpaceToken.kind == control})[0,1]
({GENDER} | {MOBILE} | {CIVIL_STATE} | {BIRTHDAY} | {EMAIL} | {HUKOU} | {POLITICAL_STATUS} | {YEARS_OF_EXPERIENCE})
-->
:nameLookup.NAME = { kind = name, conf = possible, rule = nexttootherfeatures},
:nameLookup.Section = {kind = name}

Phase: PossibleName2
Input:
Options: control = once debug = true

Rule: HasName

({NAME}):name
-->
{}

Rule: PossibleNameFarFromOthers
Priority:10

(
  {PERSON} | POSSIBLENAME
):nameLookup
{SpaceToken.kind == control}
({Sentence, !PERSON, !NAME}
 {SpaceToken.kind == control})[0,10]
({GENDER} | {MOBILE} | {CIVIL_STATE} | {BIRTHDAY} | {EMAIL} | {HUKOU} | {POLITICAL_STATUS} | {YEARS_OF_EXPERIENCE})
-->
:nameLookup.NAME = { kind = name, conf = possible, rule = nexttootherfeatures},
:nameLookup.Section = {kind = name}